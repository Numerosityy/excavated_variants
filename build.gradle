buildscript {
    dependencies {
        // to fix GSON weirdness with minotaur and FG
        classpath 'com.google.code.gson:gson:2.10'
    }
}

plugins {
    id 'dev.lukebemish.modgitpublisher' version '1.1.2'
    id 'io.github.groovymc.modsdotgroovy' version '1.1.2'
    id 'java-library'
    id 'org.quiltmc.loom' version '1.1.+' apply false
    id 'org.quiltmc.gradle.licenser' version '2.0.+' apply false
}

modGitPublisher {
    projectName = "Excavated Variants"
    minecraftVersion = project.minecraft_version
}

allprojects {p->
    if (rootProject.hasProperty('versionOverride') && rootProject.versionOverride !== null)
        p.version = rootProject.versionOverride
    else
        p.version = modGitPublisher.version
}

modsDotGroovy {
    dslVersion = '1.1.3'
    platform 'multiloader'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.quiltmc.gradle.licenser'
    
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    license {
        rule rootProject.file('header.txt')
        exclude '**/*.mcmeta'
    }
        
    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }
    
    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }

        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }

        maven {
            name 'Quilt (Release)'
            url 'https://maven.quiltmc.org/repository/release/'
        }

        maven {
            name = 'TheModdingInquisition'
            url = 'https://maven.moddinginquisition.org/releases'
        }

        maven {
            url = 'https://maven.lukebemish.dev/releases'
        }

        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

        maven {
            // location of the maven that hosts JEI files
            name = "Progwml6 maven"
            url = "https://dvs1.progwml6.com/files/maven/"
        }
        maven { url "https://maven.architectury.dev/" }
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
        implementation "com.electronwill.night-config:core:$project.nightconfig_version"
        implementation "com.electronwill.night-config:toml:$project.nightconfig_version"
        implementation "blue.endless:jankson:$project.jankson_version"
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    publishing {
        repositories {
            if (findProperty('lukebemishMavenUrl')) {
                maven {
                    name = 'PersonalMaven'
                    url = uri(findProperty('lukebemishMavenUrl'))
                    credentials(AwsCredentials) {
                        accessKey findProperty('lukebemishMavenKey') ?: ''
                        secretKey findProperty('lukebemishMavenSecret') ?: ''
                    }
                }
            }
        }
    }
}
