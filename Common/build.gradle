plugins {
    id 'maven-publish'
}

apply plugin: 'org.quiltmc.loom'

archivesBaseName = "${artifact_id}-common-${minecraft_version}"

repositories {
    maven {
        name = 'Forge / Mixin'
        url = 'https://maven.minecraftforge.net'
    }
}

loom {
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}

configurations {
    testCompileOnly.extendsFrom(compileOnly)
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_version}@zip")
    }
    //DO NOT USE IN PROJECT
    //Provides necessary stuff for project to compile
    modCompileOnly("org.quiltmc:quilt-loader:${quilt_loader_version}")

    compileOnly("dev.lukebemish.dynamicassetgenerator:dynamicassetgenerator-common-${rootProject.minecraft_version}:${rootProject.dynassetgen_version}")
    compileOnly("dev.lukebemish.defaultresources:defaultresources-common-${rootProject.minecraft_version}:${rootProject.defaultresources_version}")

    modCompileOnly("mezz.jei:jei-${rootProject.jei_minecraft_version}-common-api:${rootProject.jei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}"){transitive=false}
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
    it.targetNamespace = "named"
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
    }
}